import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'jacoco-report-aggregation'
    id 'org.springframework.boot' version '3.4.4' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version '6.1.0.5360'
}

group = 'zin.rashidi.boot'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    subprojects.forEach { p -> {
        implementation project(":${p.name}")
    }}
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testSuiteName = "test"
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "rashidi_spring-boot-tutorials"
        property "sonar.organization", "rashidi-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${getLayout().getBuildDirectory().get()}/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
    }
}

subprojects {
    apply {
        plugin('jacoco')
    }

    test {
        finalizedBy jacocoTestReport
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}