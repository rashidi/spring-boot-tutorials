import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'jacoco-report-aggregation'
    id 'org.springframework.boot' version '3.4.1' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version '6.0.1.5171'
}

group = 'zin.rashidi'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    subprojects.forEach { p -> {
        implementation project(":${p.name}")
    }}
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "rashidi_spring-boot-tutorials"
        property "sonar.organization", "rashidi-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    apply {
        plugin('jacoco')
    }

    test {
        finalizedBy jacocoTestReport
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}